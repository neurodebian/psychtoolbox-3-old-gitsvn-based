/* ExpandingRings shader
 * Draws a sequence of rings of alternating color. The center
 * of all rings is assigned in 'RingCenter' by the Matlab script,
 * width of a ring is assigned in 'RingWidth', the radius of the
 * outermost ring is assigned in 'Radius', a shift (scrolling offset)
 * is assigned in 'Shift'. See ExpandingRingsDemo.m for explanation.
 *
 *
 * Written 2007 by Mario Kleiner, licensed to you under GPL.
 */

#version 110

uniform vec2  RingCenter;
uniform float RingWidth;
uniform float Radius;
uniform float Shift;
uniform vec4  secondColor;

void main()
{
    /* Query current output texel position: */
    vec2 pos = gl_TexCoord[0].xy;

    /* Compute euclidean distance to center of our ring stim: */
    float d = distance(pos, RingCenter);

    /* If distance greater than maximum radius, discard this pixel: */
    if (d > Radius) discard;

    /* Convert distance from units of pixels into units of ringwidths, apply shift offset: */
    d = floor((d - Shift) / RingWidth);

    /* A pixel in an even-numbered ring is drawn in gl_Color, the color assigned in the */
    /* Screen('DrawTexture') command as modulateColor, whereas a pixel in an odd-numbered */
    /* ring is assigned 'secondColor': */
    gl_FragColor = mix(gl_Color, secondColor, mod(d,2.0));
}
